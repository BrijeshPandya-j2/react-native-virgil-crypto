def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    // The Android Gradle plugin is only required when opening the android folder stand-alone.
    // This avoids unnecessary downloads and potential conflicts when the library is included as a
    // module dependency in an application project.
    if (project == rootProject) {
        repositories {
            google()
            jcenter()
        }
        
        dependencies {
            classpath("com.android.tools.build:gradle:3.5.3")
        }
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 27)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 26)
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation 'com.facebook.react:react-native:'+safeExtGet('reactNativeVersion', '+')

    implementation 'com.virgilsecurity.sdk:crypto-android:6.0'

    implementation 'com.virgilsecurity.crypto:pythia-android:0.10.2'
}
  
